AC_INIT([little_heart], [0.1])
AC_PREREQ([2.69])
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_MACRO_DIR([m4])
AM_INIT_AUTOMAKE([1.9 foreign -Wall -Werror subdir-objects])
AM_PROG_AR
LT_INIT([])
AC_LANG(C++)
AC_PROG_CXX
AC_PROG_CXXCPP

AC_ARG_WITH([ibamr-source-directory],
  AS_HELP_STRING(--with-ibamr-source-directory=PATH, location of IBAMR's root directory),
  [if test ! -d "$withval"; then
     AC_MSG_ERROR(The directory specified with --with-ibamr-source-directory does not exist.)
   fi
   LH_IBAMR_SOURCE_DIRECTORY=$withval],
  [AC_MSG_ERROR(A path to the root directory of IBAMR must be supplied with --with-ibamr-source-directory=PATH.)])

AC_ARG_WITH([ibamr-build-directory],
  AS_HELP_STRING(--with-ibamr-build-directory=PATH, location of IBAMR's root directory),
  [if test ! -d "$withval"; then
     AC_MSG_ERROR(The directory specified with --with-ibamr-build-directory does not exist.)
   fi
   LH_IBAMR_BUILD_DIRECTORY=$withval],
  [AC_MSG_ERROR(A path to the build directory of IBAMR must be supplied with --with-ibamr-build-directory=PATH.)])

# Check the IBAMR source directory:
AC_CHECK_FILE($LH_IBAMR_SOURCE_DIRECTORY/include/ibamr/IBFEMethod.h,
  [],
  [AC_MSG_ERROR(The provided IBAMR directory $LH_IBAMR_SOURCE_DIRECTORY does not contain a required header file. Please ensure that IBAMR is located in this directory.)]
)

# Check the IBAMR build directory:
AC_CHECK_FILE($LH_IBAMR_BUILD_DIRECTORY/lib/libIBAMR3d.a,
  [],
  [AC_MSG_ERROR(The provided build directory $LH_IBAMR_BUILD_DIRECTORY does not appear to contain lib/libIBAMR3d.a, which is required.)])
AC_CHECK_FILE($LH_IBAMR_BUILD_DIRECTORY/config/make.inc,
  [],
  [AC_MSG_ERROR(The provided build directory $LH_IBAMR_BUILD_DIRECTORY does not appear to contain lib/libIBAMR3d.a, which is required.)])

#
# To the best of my knowledge there is no good way to export link information
# (i.e., an autotools equivalent to CMake's exported target functionality). To
# get all link information for IBAMR we thus parse (with good, old-fashioned
# grep and sed) a configuration file generated by IBAMR.
#

LH_IBAMR_ABS_TOP_BUILDDIR=$(grep '^abs_top_builddir =' $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                            sed 's/^abs_top_builddir = *//')/
AC_SUBST(LH_IBAMR_ABS_TOP_BUILDDIR, [$LH_IBAMR_ABS_TOP_BUILDDIR])

LH_IBAMR_ABS_TOP_SRCDIR=$(grep '^abs_top_srcdir =' $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                          sed 's/^abs_top_srcdir = *//')/
AC_SUBST(LH_IBAMR_ABS_TOP_SRCDIR, [$LH_IBAMR_ABS_TOP_SRCDIR])

LH_IBAMR_LDFLAGS=$(grep "^LDFLAGS =" $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                   sed "s/^LDFLAGS = *//")
AC_SUBST(LH_IBAMR_LDFLAGS, [$LH_IBAMR_LDFLAGS])

LH_IBAMR_CXXFLAGS=$(grep "^CXXFLAGS =" $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                    sed "s/^CXXFLAGS = //")
AC_SUBST(LH_IBAMR_CXXFLAGS, [$LH_IBAMR_CXXFLAGS])

LH_IBAMR_CPPFLAGS=$(grep "^CPPFLAGS =" $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                    sed "s/^CPPFLAGS = *//"                                      |
                    sed "s/-I\. / /"                                             |
                    sed "s/\$(abs_top_builddir)/\$(LH_IBAMR_ABS_TOP_BUILDDIR)/g" |
                    sed "s/\$(abs_top_srcdir)/\$(LH_IBAMR_ABS_TOP_SRCDIR)/g")
AC_SUBST(LH_IBAMR_CPPFLAGS, [$LH_IBAMR_CPPFLAGS])

LH_IBAMR_LIBS=$(grep "^LIBS =" $LH_IBAMR_BUILD_DIRECTORY/config/make.inc     |
                sed "s/^LIBS = *//"                                          |
                sed "s/-I\. / /"                                             |
                sed "s/\$(abs_top_builddir)/\$(LH_IBAMR_ABS_TOP_BUILDDIR)/g" |
                sed "s/\$(abs_top_srcdir)/\$(LH_IBAMR_ABS_TOP_SRCDIR)/g")
AC_SUBST(LH_IBAMR_LIBS, [$LH_IBAMR_LIBS])

# includes correctly-dimensioned IBTK library
LH_IBAMR_LIB_2D=$(grep "^IBAMR_LIB_2D =" $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                sed "s/^IBAMR_LIB_2D = *//"                                        |
                sed "s/\$(abs_top_builddir)/\$(LH_IBAMR_ABS_TOP_BUILDDIR)/g"       |
                sed "s/\$(abs_top_srcdir)/\$(LH_IBAMR_ABS_TOP_SRCDIR)/g")
AC_SUBST(LH_IBAMR_LIB_2D, [$LH_IBAMR_LIB_2D])

LH_IBAMR_LIB_3D=$(grep "^IBAMR_LIB_3D =" $LH_IBAMR_BUILD_DIRECTORY/config/make.inc |
                sed "s/^IBAMR_LIB_3D = *//"                                        |
                sed "s/\$(abs_top_builddir)/\$(LH_IBAMR_ABS_TOP_BUILDDIR)/g"       |
                sed "s/\$(abs_top_srcdir)/\$(LH_IBAMR_ABS_TOP_SRCDIR)/g")
AC_SUBST(LH_IBAMR_LIB_3D, [$LH_IBAMR_LIB_3D])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
